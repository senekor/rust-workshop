@_default:
    just --list --unsorted

@reset:
    echo -n reset # mark zellij frame
    # make sure reset is run after next when starting zellij
    sleep 0.2
    echo -n "" > prev.rs
    cp versions/1.rs src/main.rs

next:
    #!/bin/bash
    echo -n next # mark zellij frame
    num_versions="$(ls -q1 versions | wc -l)"
    let "upper_bound = num_versions - 1"
    for idx in $(seq 1 $upper_bound)
    do
        if [ "$(cat src/main.rs)" = "$(cat versions/$idx.rs)" ]
        then
            cp src/main.rs prev.rs
            let "idx = idx + 1"
            cp versions/$idx.rs src/main.rs
            break
        fi
    done

diff:
    watch --interval 0.2 --no-title --color difft --color=always prev.rs src/main.rs

zellij:
    zellij --layout zellij.kdl

zellij-window:
    alacritty --option 'font.size=24' --command just zellij &
